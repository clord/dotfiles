#!/usr/bin/env zsh

setopt prompt_subst

autoload -U vcs_info

# Usage: color TEXT [COLOR_GOOD] [COLOR_BAD]
# Will color text with COLOR_GOOD.
# If COLOR_BAD is given, it will be used when there is an error.
color()
{
    local text=$1; shift
    case $# in
        2) echo "%(0?.%F{$1}.%F{$2})${text}%f" ;;
        1) echo "%F{$1}${text}%f" ;;
        *) echo "$text" ;;
    esac
}

colors=$(tput colors)  # This better be 256

str_a='%a'      # Action, if we are in the middle of one
str_b='%b%u%c'  # Branch + staged/unstaged changes
str_d='%~'      # PWD, shortened, if possible
str_m='%m'      # Machine name
str_n='%n'      # User name
str_p='›'       # Prompt
str_s='+'       # This represents staged changes
str_u='!'       # This represents unstaged changes
str_v='%s'      # The VCS in use

# 0 = Black
# 1 = Red
# 2 = Green
# 3 = Yellow
# 4 = Blue
# 5 = Magenta
# 6 = Cyan
# 7 = White

case $colors in
    256)
        str_a=$(color $str_a  4    )
        str_b=$(color $str_b 72 214)
        str_d=$(color $str_d 84 226)
        str_m=$(color $str_m 54 196)
        str_n=$(color $str_n 60 202)
        str_s=$(color $str_s 10    )
        str_u=$(color $str_u  9    )
        str_v=$(color $str_v 66 208)
        ;;
    8)
        str_a=$(color $str_a 5  )
        str_b=$(color $str_b 6 3)
        str_d=$(color $str_d 2  )
        str_m=$(color $str_m 5 1)
        str_n=$(color $str_n 5 1)
        str_s=$(color $str_s 2  )
        str_u=$(color $str_u 1  )
        str_v=$(color $str_v 6 3)
        ;;
esac

str_format="· ${str_v} ${str_b}"
str_format=${str_format//\%\(/%%(}

zstyle ':vcs_info:*' enable git hg bzr svn
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr     "$str_s"
zstyle ':vcs_info:*' unstagedstr   "$str_u"
zstyle ':vcs_info:*' formats       "$str_format "
zstyle ':vcs_info:*' actionformats "$str_format $str_a "

virtualenv_info()
{
    unset virtualenv_info
    [[ -z $VIRTUAL_ENV ]] && return
    virtualenv_info=" ${VIRTUAL_ENV##*/}"
}

precmd_functions+='vcs_info'
precmd_functions+='virtualenv_info'

# Time to stack the blocks!

PROMPT=
PROMPT+="%B"
PROMPT+="$str_m "
PROMPT+="$str_n "
PROMPT+='${vcs_info_msg_0_}'
PROMPT+="$str_p "
PROMPT+="%b"

RPROMPT=
RPROMPT+="%B"
RPROMPT+="$str_d"
RPROMPT+='${virtualenv_info}'
RPROMPT+="%b"
