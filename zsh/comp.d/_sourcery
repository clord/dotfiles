#compdef sourcery

local context state line config_dir
typeset -A opt_args

get_config_dir()
{
    for value in $config_dir $CONFIG_DIR $HOME/.sourcery; do
        if [[ -n $value ]]; then
            echo $value
            return
        fi
    done
}

_arguments -s -S \
    "-h[Display help message]" \
    "-i[Interactive mode]" \
    "-f[Non-interactive mode]" \
    "-v[Verbose mode]" \
    "-q[Non-verbose mode]" \
    "-C[Specify config directory]:config directory:_files -/" \
    "-P[Specify profile]:profile:->profile" \
    "-N[Create profile]" \
    "-L[List profiles]" \
    "-D[Delete profile]" \
    "-E[Edit profile]" \
    "-a[Use all profiles]" \
    "-t[Truncate old archive files]" \
    "-m[Specify message for commit]:message:" \
    "-A[List available actions]" \
    "*::action:->action" \
    && return 0

(( $+opt_args[-C] )) && config_dir=$opt_args[-C]

case $state in
    action)
        _values 'actions' $(sourcery -C $(get_config_dir) -A)
        ;;
    profile)
        _values 'profiles' $(sourcery -C $(get_config_dir) -L)
        ;;
esac
